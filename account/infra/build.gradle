plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.google.cloud.tools.jib' version '3.4.0'
    id 'io.freefair.lombok' version '8.4'
    id 'jacoco'
}

archivesBaseName = 'account-infra'

sourceCompatibility = JavaVersion.VERSION_21

dependencies {
    implementation project(':shared:infra')
    implementation project(':account:domain')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation "org.postgresql:postgresql:$rootProject.postgresDriverVersion"
    implementation "org.liquibase:liquibase-core:$rootProject.liquibaseVersion"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign:$rootProject.feignVersion"
    implementation "org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j:$rootProject.circuitBreakerVersion"
    implementation "org.springframework.cloud:spring-cloud-kubernetes:$rootProject.springCloudKubernetesVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$rootProject.junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$rootProject.junitVersion"
    testImplementation 'org.testcontainers:postgresql'
    testImplementation "org.springframework.cloud:spring-cloud-contract-stub-runner:4.1.0"
    testImplementation files(
            "${project.rootDir}/order/infra/build/libs/infra-stubs.jar",
            "${project.rootDir}/payment/infra/build/libs/infra-stubs.jar"
    )
}

tasks.compileTestJava.dependsOn rootProject.tasks.findByPath('order:infra:verifierStubsJar')
tasks.compileTestJava.dependsOn rootProject.tasks.findByPath('payment:infra:verifierStubsJar')

jib {
    from {
        image = "eclipse-temurin:21.0.1_12-jre-jammy"
        platforms {
            platform {
                os = "linux"
                architecture = "amd64"
            }
            platform {
                os = "linux"
                architecture = "arm64"
            }
        }
    }
    to {
        image = "docker.io/ahmsay/solidvessel-account"
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}
